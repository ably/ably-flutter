group 'io.ably.flutter.plugin'
// Use the snapshot suffix to show that build artifacts are not shared.
// This Gradle file is used by Flutter to build itself.
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.12.1'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

dependencies {
    // https://github.com/ably/ably-java/
    implementation 'io.ably:ably-android:1.2.40'

    // https://firebase.google.com/docs/cloud-messaging/android/client
    implementation 'com.google.firebase:firebase-messaging:23.0.6'

    // https://github.com/google/desugar_jdk_libs
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

android {
    // Older versions of the Flutter SDK don't provide `compileSdkVersion`, so it defaults to 34
    compileSdkVersion flutter.compileSdkVersion ?: 34

    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'io.ably.flutter.plugin'
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    defaultConfig {
        minSdkVersion 19
        // Required when setting minSdkVersion to 20 or lower so we can use
        // the Java 8 language APIs as mentioned here:
        // https://developer.android.com/studio/write/java8-support#library-desugaring
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    buildFeatures {
        buildConfig true
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

apply from: file("./ably-agent.gradle")
